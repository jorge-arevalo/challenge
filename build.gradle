plugins {
	id 'org.springframework.boot' version '2.7.11'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id 'org.sonarqube' version '4.0.0.2929'
	id 'org.liquibase.gradle' version '2.2.0'
	id 'java'
	id 'jacoco'
}

java {
	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11
}

group = 'com.globank.management'
version = '0.0.1-SNAPSHOT'

sonarqube {
	properties {
		property "sonar.projectKey", "com.bank.management:challenge"
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.postgresql:postgresql:42.6.0'
	implementation 'com.h2database:h2'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-log4j2:2.7.11'
	implementation 'org.springdoc:springdoc-openapi-ui:1.7.0'

	annotationProcessor 'org.projectlombok:lombok'

	compileOnly 'org.projectlombok:lombok'

	testImplementation 'org.liquibase.ext:liquibase-hibernate5:4.21.1'
	testImplementation 'org.postgresql:postgresql:42.6.0'
	testImplementation "org.testcontainers:postgresql:1.18.0"
	testImplementation "org.testcontainers:junit-jupiter:1.18.0"

	testImplementation 'io.cucumber:cucumber-java'
	testImplementation 'io.cucumber:cucumber-spring'
	testImplementation 'io.cucumber:cucumber-junit-platform-engine'
	testImplementation 'org.junit.platform:junit-platform-suite'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'

	liquibaseRuntime 'org.liquibase:liquibase-core:4.21.1'
	liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:3.0.3'
	liquibaseRuntime 'info.picocli:picocli:4.7.3'
	liquibaseRuntime 'org.liquibase.ext:liquibase-hibernate5:4.21.1'
	liquibaseRuntime 'software.aws.rds:aws-postgresql-jdbc:0.1.0'
	liquibaseRuntime sourceSets.main.output

}

configurations {
	liquibaseRuntime.extendsFrom implementation
	configureEach {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
	}

}

dependencyManagement {
	imports {
		mavenBom 'io.cucumber:cucumber-bom:7.12.0'
	}
	dependencies {
		dependency 'com.fasterxml.jackson.core:jackson-databind:2.15.0'
		dependency 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.0'
	}
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test // tests are required to run before generating the reports
	reports {
		xml.enabled true
		csv.enabled false
		html.enabled true
	}
}
